# Note: Requires http://www.microsoft.com/en-us/download/details.aspx?id=29

# Initial stuff
!define APP_NAME SyncthingGTK
!define LIBRARIES_FILE "syncthing-gtk-windows-libraries-0.4.3.zip"
!define LIBRARIES_URL "http://kozec.com/${LIBRARIES_FILE}"
!define MSVSLIBS_FILE "vcredist_x86.exe"
!define MSVSLIBS_URL "http://download.microsoft.com/download/1/1/1/1116b75a-9ec3-481a-a3c8-1777b5381140/${MSVSLIBS_FILE}"
OutFile "${APP_NAME}-win32-installer.exe"
InstallDir $PROGRAMFILES\${APP_NAME}
 
# Install
Section
	SetOutPath $INSTDIR
	# Install good stuff
	File build\exe.win32-2.7\syncthing-gtk.exe
	File build\exe.win32-2.7\library.zip
	File build\exe.win32-2.7\*.glade
	File /r build\exe.win32-2.7\icons
	# Check if random file that should be part of libraries zip exists
	# and download&extract zip if not
	IfFileExists $INSTDIR\libgmodule-2.0-0.dll SkipDownload DoDownload
DoDownload:
	NSISdl::download ${LIBRARIES_URL} $TEMP\${LIBRARIES_FILE}
	ZipDLL::extractall $TEMP\${LIBRARIES_FILE} $INSTDIR
SkipDownload:
	# Write out installation location
	WriteRegStr HKCU SOFTWARE\${APP_NAME} 'InstallPath' '$INSTDIR'
	# Create shortcut
	CreateShortCut "$SMPROGRAMS\Syncthing GTK.lnk" "$INSTDIR\syncthing-gtk.exe" "" "$INSTDIR\icons\st-logo-128.ico"
	# Create uninstaller
	WriteUninstaller $INSTDIR\uninstaller.exe
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\SyncthingGTK" \
		"DisplayName" "Syncthing GTK"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\SyncthingGTK" \
		"UninstallString" "$INSTDIR\uninstaller.exe"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\SyncthingGTK" \
		"DisplayIcon" "$INSTDIR\uninstaller.exe"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\SyncthingGTK" \
		"Publisher" "Kozec"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\SyncthingGTK" \
		"URLInfoAbout" "https://github.com/kozec/syncthing-gui/"
	WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\SyncthingGTK" \
		"NoRepair" 1
	WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\SyncthingGTK" \
		"NoModify" 1
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\SyncthingGTK" \
		"DisplayIcon" "$INSTDIR\icons\st-logo-128.ico"
		
SectionEnd

# Uninstall
Section "Uninstall"
	Delete $INSTDIR\uninstaller.exe
	Delete $INSTDIR\*
	RMDir /r $INSTDIR
	DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\SyncthingGTK"
SectionEnd
