# Required plugins: NSISdl, ZipDLL, FindProcDLL (by hnedka)

# Initial stuff
!define APP_NAME SyncthingGTK
!define LIBRARIES_FILE "syncthing-gtk-windows-libraries-0.5.3.zip"
!define LIBRARIES_URL "http://kozec.com/${LIBRARIES_FILE}"
!include "build\version.nsh"

OutFile "${APP_NAME}-${VERSION}-installer.exe"
InstallDir $PROGRAMFILES\${APP_NAME}

Page directory
Page instfiles

# Install
Section
	# Check if app is already running
	FindProcDLL::FindProc "syncthing-gtk.exe"
	IntCmp $R0 1 0 NotRunning
		MessageBox MB_OK|MB_ICONEXCLAMATION "Syncthing-GTK is running. Please close it first." /SD IDOK
		Abort
NotRunning:
	# Set output path
	SetOutPath $INSTDIR
	# Install good stuff
	File build\exe.win32-2.7\syncthing-gtk.exe
	File build\exe.win32-2.7\library.zip
	File build\exe.win32-2.7\*.glade
	File build\exe.win32-2.7\__version__
	File /r build\exe.win32-2.7\icons
	# Check if random file that should be part of libraries zip exists
	# and download&extract zip if not
	IfFileExists $INSTDIR\share\glib-2.0\schemas\org.gtk.Settings.FileChooser.gschema.xml SkipDownload DoDownload
DoDownload:
	NSISdl::download ${LIBRARIES_URL} $TEMP\${LIBRARIES_FILE}
	ZipDLL::extractall $TEMP\${LIBRARIES_FILE} $INSTDIR
SkipDownload:
	# Write out installation location
	WriteRegStr HKCU SOFTWARE\${APP_NAME} 'InstallPath' '$INSTDIR'
	# Create shortcut
	CreateShortCut "$SMPROGRAMS\Syncthing GTK.lnk" "$INSTDIR\syncthing-gtk.exe" ""
	# Create uninstaller
	WriteUninstaller $INSTDIR\uninstaller.exe
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\SyncthingGTK" \
		"DisplayName" "Syncthing GTK"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\SyncthingGTK" \
		"UninstallString" "$INSTDIR\uninstaller.exe"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\SyncthingGTK" \
		"DisplayIcon" "$INSTDIR\uninstaller.exe"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\SyncthingGTK" \
		"Publisher" "Kozec"
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\SyncthingGTK" \
		"URLInfoAbout" "https://github.com/kozec/syncthing-gui/"
	WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\SyncthingGTK" \
		"NoRepair" 1
	WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\SyncthingGTK" \
		"NoModify" 1
	WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\SyncthingGTK" \
		"DisplayIcon" "$INSTDIR\icons\st-logo-128.ico"
		
SectionEnd

# Uninstall
Section "Uninstall"
	FindProcDLL::FindProc "syncthing-gtk.exe"
	IntCmp $R0 1 0 NotRunning
		MessageBox MB_OK|MB_ICONEXCLAMATION "Syncthing-GTK is running. Please close it first." /SD IDOK
		Abort
NotRunning:
	Delete $INSTDIR\uninstaller.exe
	Delete $INSTDIR\*
	RMDir /r $INSTDIR
	DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\SyncthingGTK"
SectionEnd
